# This file maps the CMSIS project options to toolchain settings.
#
#   - Applies to toolchain: Microchip XC32 Compiler version 5.0.0 and greater

set(AS "xc32-gcc")
set(CC "xc32-gcc")
set(CPP "xc32-gcc")
set(CXX "xc32-g++")
set(OC "xc32-objcopy")

set(TOOLCHAIN_ROOT "${REGISTERED_TOOLCHAIN_ROOT}")
set(TOOLCHAIN_VERSION "${REGISTERED_TOOLCHAIN_VERSION}")

if(DEFINED TOOLCHAIN_ROOT)
  set(PREFIX)
  set(EXT)
  
  set(AS ${TOOLCHAIN_ROOT}/${PREFIX}${AS}${EXT})
  set(CC ${TOOLCHAIN_ROOT}/${PREFIX}${CC}${EXT})
  set(CPP ${TOOLCHAIN_ROOT}/${PREFIX}${CPP}${EXT})
  set(CXX ${TOOLCHAIN_ROOT}/${PREFIX}${CXX}${EXT})
  set(OC ${TOOLCHAIN_ROOT}/${PREFIX}${OC}${EXT})
endif()

# Helpers

function(cbuild_set_option_flags lang option value flags)
  if(NOT DEFINED ${option}_${lang}_FLAGS)
    return()
  endif()
  list(FIND ${option}_VALUES "${value}" _index)
  if (${_index} GREATER -1)
    list(GET ${option}_${lang}_FLAGS ${_index} flag)
    set(${flags} "${flag} ${${flags}}" PARENT_SCOPE)
  elseif(NOT value STREQUAL "")
    string(TOLOWER "${option}" _option)
    message(FATAL_ERROR "unknown '${_option}' value '${value}' !")
  endif()
endfunction()

function(cbuild_set_options_flags lang optimize debug warnings language flags)
  set(tmp)
  cbuild_set_option_flags(${lang} OPTIMIZE "${optimize}" tmp)
  cbuild_set_option_flags(${lang} DEBUG    "${debug}"    tmp)
  cbuild_set_option_flags(${lang} WARNINGS "${warnings}" tmp)
  cbuild_set_option_flags(${lang} LANGUAGE "${language}" tmp)
  set(${flags} "${tmp} ${${flags}}" PARENT_SCOPE)
endfunction()

function(cbuild_set_defines lang defines)
  set(TMP_DEFINES)
  foreach(DEFINE ${${defines}})
    string(REPLACE "\"" "\\\"" ENTRY ${DEFINE})
    string(REGEX REPLACE "=.*" "" KEY ${ENTRY})
    if (KEY STREQUAL ENTRY)
      set(VALUE "1")
    else()
      string(REGEX REPLACE ".*=" "" VALUE ${ENTRY})
    endif()
    if(${lang} STREQUAL "AS_LEG")
      string(APPEND TMP_DEFINES "--defsym ${KEY}=${VALUE} ")
    elseif(${lang} STREQUAL "AS_GNU")
      string(APPEND TMP_DEFINES "-Wa,-defsym,\"${KEY}=${VALUE}\" ")
    else()
      string(APPEND TMP_DEFINES "-D${ENTRY} ")
    endif()
  endforeach()
  set(${defines} ${TMP_DEFINES} PARENT_SCOPE)
endfunction()

# XC32 Processor/DFP flags
if(DPACK MATCHES "Microchip")
  set(MDFP "")
  file(GLOB_RECURSE ALL_DIRS LIST_DIRECTORIES true "${DPACK_DIR}/*")
  foreach(DIR_PATH ${ALL_DIRS})
      if(IS_DIRECTORY "${DIR_PATH}" AND DIR_PATH MATCHES "xc32/")
	      get_filename_component(CURRENT_DIR_NAME "${DIR_PATH}" NAME)
	      if(CURRENT_DIR_NAME STREQUAL DNAME AND EXISTS "${DIR_PATH}/specs-${DNAME}")
		      set(MDFP "${DIR_PATH}/../../")
		      break()
	      endif()
      endif()
  endforeach()

  if(NOT DEFINED MDFP OR MDFP STREQUAL "")
	    message(FATAL_ERROR " Error: Could not determine the DFP path for -mdfp option!!")
  endif()
  set(XC32_COMMON_FLAGS "-mprocessor=${DNAME} -mdfp=${MDFP}")
else()
  set(XC_MODE "-mthumb")
  if(CPU MATCHES "ARM9")
      set(HAS_FPU_HARDWARE FALSE)
      set(XC_MODE "-marm")
  elseif(CPU MATCHES "Cortex-M0")
      if(CPU MATCHES "Cortex-M0+")
        set(CPU "Cortex-M0plus")
      endif()
      set(HAS_FPU_HARDWARE FALSE)
  elseif(CPU STREQUAL "Cortex-M3")
      set(HAS_FPU_HARDWARE FALSE)
  elseif(CPU MATCHES "Cortex-M4|Cortex-M7")
      set(HAS_FPU_HARDWARE TRUE)
  elseif(CPU MATCHES "Cortex-M23")
      set(HAS_FPU_HARDWARE FALSE)
  elseif(CPU MATCHES "Cortex-M33")
      set(HAS_FPU_HARDWARE TRUE)
  elseif(CPU MATCHES "Cortex-A")
      set(HAS_FPU_HARDWARE TRUE)
      set(XC_MODE "-marm")
  else()
      message(FATAL_ERROR "Error: CPU not supported by XC32!!")
  endif()

  if(FPU STREQUAL "NOT_FPU" OR NOT FPU OR NOT HAS_FPU_HARDWARE)
      set(XC_MABI "soft")
  else()
      set(XC_MABI "hard")
  endif()

  set(XC32_COMMON_FLAGS "-forceArch=pic32c ${XC_MODE} -mcpu=${CPU} -mfloat-abi=${XC_MABI}")

endif()

set(OPTIMIZE_VALUES       "debug" "none" "balanced" "size" "speed")
set(OPTIMIZE_CC_FLAGS     "-Og"   "-O0"  "-O2"      "-Os"  "-O1")
set(OPTIMIZE_CXX_FLAGS    ${OPTIMIZE_CC_FLAGS})
set(OPTIMIZE_LD_FLAGS     ${OPTIMIZE_CC_FLAGS})
set(OPTIMIZE_ASM_FLAGS    ${OPTIMIZE_CC_FLAGS})
set(OPTIMIZE_AS_GNU_FLAGS ${OPTIMIZE_CC_FLAGS})

set(DEBUG_VALUES          "on"    "off")
set(DEBUG_CC_FLAGS        "-g"    "")
set(DEBUG_CXX_FLAGS       "-g"    "")
set(DEBUG_LD_FLAGS        "-g"    "")
set(DEBUG_ASM_FLAGS       "-g"    "")
set(DEBUG_AS_GNU_FLAGS    "-g"    "")

set(WARNINGS_VALUES       "on"     "off"       "all")
set(WARNINGS_CC_FLAGS     "-Wall"  "-w"        "-Wall -Wextra")
set(WARNINGS_CXX_FLAGS    "-Wall"  "-w"        "-Wall -Wextra")
set(WARNINGS_ASM_FLAGS    "-Wall"  "-w"        "-Wall -Wextra")
set(WARNINGS_AS_GNU_FLAGS "-Wall"  "-w"        "-Wall -Wextra")
set(WARNINGS_LD_FLAGS     ""       ""          "")

set(LANGUAGE_VALUES       "c90"      "gnu90"      "c99"      "gnu99"      "c11"      "gnu11"       ""    ""    "c++98"      "gnu++98"      "c++03"      "gnu++03"      "c++11"      "gnu++11"      "c++14"      "gnu++14"      "c++17"      "gnu++17"      "" "" "" "" )
set(LANGUAGE_CC_FLAGS     "-std=c90" "-std=gnu90" "-std=c99" "-std=gnu99" "-std=c11" "-std=gnu11"  ""    ""    ""           ""             ""           ""             ""           ""             ""           ""             ""           ""             "" "" "" "" )
set(LANGUAGE_CXX_FLAGS    ""         ""           ""         ""           ""         ""            ""    ""    "-std=c++98" "-std=gnu++98" "-std=c++03" "-std=gnu++03" "-std=c++11" "-std=gnu++11" "-std=c++14" "-std=gnu++14" "-std=c++17" "-std=gnu++17" "" "" "" "" )

# Assembler

set(AS_LEG_CPU ${XC32_COMMON_FLAGS})
set(AS_GNU_CPU ${XC32_COMMON_FLAGS})
set(ASM_CPU ${XC32_COMMON_FLAGS})

set(AS_LEG_FLAGS)
set(AS_GNU_FLAGS "-c")
set(ASM_FLAGS "-c")

set(AS_LEG_DEFINES ${DEFINES})
cbuild_set_defines(AS_LEG AS_LEG_DEFINES)
set(AS_GNU_DEFINES ${DEFINES})
cbuild_set_defines(AS_GNU AS_GNU_DEFINES)
set(ASM_DEFINES ${DEFINES})
cbuild_set_defines(ASM ASM_DEFINES)

set(AS_LEG_OPTIONS_FLAGS)
cbuild_set_options_flags(AS_LEG "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "" AS_LEG_OPTIONS_FLAGS)
set(AS_GNU_OPTIONS_FLAGS)
cbuild_set_options_flags(AS_GNU "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "" AS_GNU_OPTIONS_FLAGS)
set(ASM_OPTIONS_FLAGS)
cbuild_set_options_flags(ASM "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "" ASM_OPTIONS_FLAGS)

if(BYTE_ORDER STREQUAL "Little-endian")
  set(ASM_BYTE_ORDER "-mlittle-endian")
elseif(BYTE_ORDER STREQUAL "Big-endian")
  set(ASM_BYTE_ORDER "-mbig-endian")
endif()
set(AS_LEG_BYTE_ORDER "${ASM_BYTE_ORDER}")
set(AS_GNU_BYTE_ORDER "${ASM_BYTE_ORDER}")

#C Pre-Processor

if(SECURE STREQUAL "Secure" OR SECURE STREQUAL "Secure-only")
  set(CC_SECURE "-mcmse")
endif()

set(CPP_FLAGS "-E -P ${XC32_COMMON_FLAGS} -xc ${CC_SECURE}")
set(CPP_DEFINES ${LD_SCRIPT_PP_DEFINES})
cbuild_set_defines(CC CPP_DEFINES)
if(DEFINED LD_REGIONS AND NOT LD_REGIONS STREQUAL "")
  set(CPP_INCLUDES "-include \"${LD_REGIONS}\"")
endif()
set(CPP_ARGS_LD_SCRIPT "${CPP_FLAGS} ${CPP_DEFINES} ${CPP_INCLUDES} \"${LD_SCRIPT}\" -o \"${LD_SCRIPT_PP}\"")
separate_arguments(CPP_ARGS_LD_SCRIPT NATIVE_COMMAND ${CPP_ARGS_LD_SCRIPT})

# C Compiler

set(CC_CPU "${XC32_COMMON_FLAGS}")
set(CC_DEFINES ${ASM_DEFINES})
set(CC_BYTE_ORDER ${ASM_BYTE_ORDER})
set(CC_FLAGS)
set(CC_OPTIONS_FLAGS)
set(CC_LTO "-flto")
set(_PI "-include ")
set(_ISYS "-isystem ")
cbuild_set_options_flags(CC "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "${LANGUAGE_CC}" CC_OPTIONS_FLAGS)

if(BRANCHPROT STREQUAL "NO_BRANCHPROT")
  set(CC_BRANCHPROT "-mbranch-protection=none")
elseif(BRANCHPROT STREQUAL "BTI")
  set(CC_BRANCHPROT "-mbranch-protection=bti")
elseif(BRANCHPROT STREQUAL "BTI_SIGNRET")
  set(CC_BRANCHPROT "-mbranch-protection=bti+pac-ret")
endif()

# C++ Compiler

set(CXX_CPU "${CC_CPU}")
set(CXX_DEFINES "${CC_DEFINES}")
set(CXX_BYTE_ORDER "${CC_BYTE_ORDER}")
set(CXX_FLAGS "${CC_FLAGS}")
set(CXX_OPTIONS_FLAGS)
set(CXX_LTO "${CC_LTO}")
set(CXX_SECURE "${CC_SECURE}")
set(CXX_BRANCHPROT "${CC_BRANCHPROT}")
cbuild_set_options_flags(CXX "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "${LANGUAGE_CXX}" CXX_OPTIONS_FLAGS)

# Linker

set(LD_CPU ${XC32_COMMON_FLAGS})
set(_LS "-T ")
set(LD_FLAGS)
set(LD_OPTIONS_FLAGS)
cbuild_set_options_flags(LD "${OPTIMIZE}" "${DEBUG}" "${WARNINGS}" "" LD_OPTIONS_FLAGS)

# ELF to HEX conversion
set (ELF2HEX -O ihex "${OUT_DIR}/$<TARGET_PROPERTY:${TARGET},OUTPUT_NAME>$<TARGET_PROPERTY:${TARGET},SUFFIX>" "${OUT_DIR}/${HEX_FILE}")

# ELF to BIN conversion
set (ELF2BIN -O binary "${OUT_DIR}/$<TARGET_PROPERTY:${TARGET},OUTPUT_NAME>$<TARGET_PROPERTY:${TARGET},SUFFIX>" "${OUT_DIR}/${BIN_FILE}")

# Set CMake variables for toolchain initialization
set(CMAKE_C_FLAGS_INIT "${CC_CPU}")
set(CMAKE_CXX_FLAGS_INIT "${CXX_CPU}")
set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_CROSSCOMPILING TRUE)
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)
set(CMAKE_ASM_COMPILER "${CC}")
set(CMAKE_AS_LEG_COMPILER "${AS}")
set(CMAKE_AS_GNU_COMPILER "${CC}")
set(CMAKE_C_COMPILER "${CC}")
set(CMAKE_CXX_COMPILER "${CXX}")
set(CMAKE_OBJCOPY "${OC}")
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/CMakeASM")
